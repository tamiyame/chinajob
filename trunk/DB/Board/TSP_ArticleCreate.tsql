set ANSI_NULLS ON
set QUOTED_IDENTIFIER ON
GO

/* *******************************************************************
** DATE :
** Writer :
** Memo :
******************************************************************** */
ALTER PROCEDURE [dbo].[TSP_ArticleCreate]
(
	@IsTransaction	tinyint			= 0
,	@ErrorCode		int				= 0		OUTPUT
,	@ErrorMessage	nvarchar( 1000 )= ''	OUTPUT

,	@BoardNo		int
,	@ArticleNo_parent	int	= null
,	@BoardCategoryNo	int	= null
,	@BoardSubCategoryNo	int	= null
,	@UserNo			int
,	@UserID			varchar( 24 )
,	@UserName		nvarchar( 50 )
,	@UserCNName		nvarchar( 50 )	= null
,	@UserENGName		nvarchar( 50 )	= null
,	@UserIP			varchar( 20 )

,	@ArticleTitle	nvarchar( 500 )
,	@SomeContent	nvarchar( 2000 )
,	@ArticleContent	nvarchar( max )

,	@ArticleNo		int	= null output
)
as
Set NOCOUNT ON

-- Frk Default Value
DECLARE @FrkDateNow DateTime
DECLARE @FrkRowCount int
DECLARE @FrkTranCount int
DECLARE @FrkErrorCode int
DECLARE @FrkErrorMessage nvarchar( 1000 )
Set @FrkDateNow = GetDate()
set @FrkTranCount = @@TRANCOUNT
-- Frk Check


If ( @IsTransaction = 0 )
Begin
	If ( @FrkTranCount < 1 )
	Begin
		Set @FrkErrorCode = -1
		Set @FrkErrorMessage = 'Transaction.'
		goto ERROR
	End
	Else If ( @FrkTranCount > 1 )
	Begin
		Set @FrkErrorCode = -2
		Set @FrkErrorMessage = 'Transaction.'
		goto ERROR
	End
End
Else
Begin
	If ( @FrkTranCount <> 0 )
	Begin
		Set @FrkErrorCode = -3
		Set @FrkErrorMessage = 'Transaction.'
		goto ERROR
	End
	Begin Tran
End

-- Implementation

DECLARE @ArticleGroupNo int
DECLARE @OrderNo smallint
DECLARE @LevelNo tinyint


IF ( @ArticleNo_parent Is Not Null )
BEGIN
	SELECT
		@ArticleGroupNo = ArticleGroupNo
	,	@OrderNo = OrderNo
	,	@LevelNo = LevelNo + 1
	FROM
		TB_Article WITH ( Nolock )
	WHERE
		ArticleNo = @ArticleNo_parent
	
	UPDATE
		TB_Article
	SET
		OrderNo = OrderNo + 1
	WHERE
		ArticleGroupNo = @ArticleGroupNo
	AND	OrderNo >= @OrderNo
	
	UPDATE
		TB_Board
	SET
		@ArticleNo = MaxArticleNo = MaxArticleNo + 1
	,	ArticleCreate = ArticleCreate + 1
	WHERE
		BoardNo = @BoardNo
END
ELSE
BEGIN
	UPDATE
		TB_Board
	SET
		@ArticleNo = MaxArticleNo = MaxArticleNo + 1
	,	@ArticleGroupNo = MaxArticleGroupNo = MaxArticleGroupNo + 1
	,	ArticleCreate = ArticleCreate + 1
	WHERE
		BoardNo = @BoardNo
	
	SET @OrderNo = 1
	SET @LevelNo = 0
END

IF ( @BoardCategoryNo Is Not Null )
BEGIN
	UPDATE
		TB_BoardCategory
	SET
		ArticleCreate = ArticleCreate + 1
	WHERE
		BoardNo = @BoardNo
	AND	BoardCategoryNo = @BoardCategoryNo
END

IF ( @BoardCategoryNo Is Not Null AND @BoardSubCategoryNo Is Not Null)
BEGIN
	UPDATE
		TB_BoardSubCategory
	SET
		ArticleCreate = ArticleCreate + 1
	WHERE
		BoardNo = @BoardNo
	AND	BoardCategoryNo = @BoardCategoryNo
	AND	BoardSubCategoryNo = @BoardSubCategoryNo
END

INSERT	TB_Article
(
	BoardNo
,	ArticleNo
,	ArticleNo_parent
,	ArticleGroupNo
,	OrderNo
,	LevelNo
,	BoardCategoryNo
,	BoardSubCategoryNo
,	UserNo
,	UserID
,	UserName
,	UserCNName
,	UserENGName
,	UserIP
,	ArticleTitle
,	SomeContent
,	ArticleContent
,	ReadCount
,	DateCreated
,	CommentCreate
,	CommentRemove
,	MaxCommentNo
,	Status
)
SELECT
	@BoardNo
,	@ArticleNo
,	@ArticleNo_parent
,	@ArticleGroupNo
,	@OrderNo
,	@LevelNo
,	@BoardCategoryNo
,	@BoardSubCategoryNo
,	@UserNo
,	@UserID
,	@UserName
,	@UserCNName
,	@UserENGName
,	@UserIP
,	@ArticleTitle
,	@SomeContent
,	@ArticleContent
,	0		ReadCount
,	@FrkDateNow DateCreated
,	0	CommentCreate
,	0	CommentRemove
,	0	MaxCommentNo
,	1	Status





-- Frk Error Check
SELECT
	@FrkErrorCode = @@ERROR
,	@FrkRowCount = @@RowCount

-- End

COMPLETE:
	If ( @IsTransaction <> 0 )
	Begin
		If ( @@TRANCOUNT = 1 )
		Begin
			Commit Tran
		End
	End
	SET @ErrorCode = 0
	SET @ErrorMessage = ''
	return 0
ERROR:
	If ( @IsTransaction <> 0 )
	Begin
		If ( @@TRANCOUNT = 1 )
		Begin
			Rollback Tran
		End
	End
	SET @ErrorCode = @FrkErrorCode
	SET @ErrorMessage = @FrkErrorMessage
	return @FrkErrorCode



